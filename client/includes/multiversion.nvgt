/*
MIT License

Copyright (c) 2024 [Harry Min Khant](https://harrymkt.github.io)

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/
// Multiversion, a script to check version compare.
class multiversion {
	int[] vers;
	private string ver;

	// Constructor
	multiversion(string input) {
		this.version = input;
	}

	private void setup(string inp) {
		this.vers.resize(0);
		string[] split = inp.split(".");
		for (uint i = 0; i < split.length(); i++)
			vers.insert_last(parse_int(split[i].trim_whitespace()));
	}

	int opIndex(uint n) {
		return this.vers[n];
	}

	uint length() {
		return this.vers.length();
	}

	// Compare versions with segment scaling
	int opCmp(multiversion@ other) {
		uint maxLen = (this.length() > other.length()) ? this.length() : other.length();

		for (uint i = 0; i < maxLen; i++) {
			int a = (i < this.length()) ? this[i] : 0;
			int b = (i < other.length()) ? other[i] : 0;

			// Determine number of digits for scaling
			uint lenA = string(a).length();
			uint lenB = string(b).length();
			uint scale = (lenA > lenB) ? lenA : lenB;

			// Scale each segment up to match the largest segment length
			int scaledA = a;
			int scaledB = b;
			for (uint j = 0; j < scale - lenA; j++) scaledA *= 10;
			for (uint j = 0; j < scale - lenB; j++) scaledB *= 10;

			if (scaledA > scaledB) return 1;
			else if (scaledA < scaledB) return -1;
		}
		return 0; // equal
	}

	string get_version() property {
		return this.ver;
	}

	void set_version(string value) property {
		value.trim_whitespace_this();
		this.ver = value;
		this.setup(value);
	}
}
